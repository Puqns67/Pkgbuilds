# Maintainer: Puqns67 <me@puqns67.icu>

_pkgname='alist'
pkgname="${_pkgname}-bin"
pkgver=3.29.0
pkgrel=1
pkgdesc='A file list/WebDAV program that supports multiple storages, powered by Gin and Solidjs.'
url="https://github.com/alist-org/alist"
license=('AGPL3')
arch=('aarch64' 'x86_64' 'riscv64')
provides=("${_pkgname}")
conflicts=("${_pkgname}")

options=('!strip')

source=("${_pkgname}.service"
        "${_pkgname}-sysusers.conf"
        "${_pkgname}-tmpfiles.conf")
source_aarch64=("${_pkgname}-${pkgver}-aarch64.tar.gz"::"${url}/releases/download/v${pkgver}/${_pkgname}-linux-arm64.tar.gz")
source_x86_64=("${_pkgname}-${pkgver}-x86_64.tar.gz"::"${url}/releases/download/v${pkgver}/${_pkgname}-linux-amd64.tar.gz")
source_riscv64=("${_pkgname}-${pkgver}-riscv64.tar.gz"::"${url}/releases/download/v${pkgver}/${_pkgname}-linux-riscv64.tar.gz")

sha512sums=('e8bfd6079626799ca7af015550c5605991631460d18be2aab41d58412488fd17672b9125c47cfb019d519d883d6b89e4f2f367eddfb30d5857e29fd91f582f78'
            '923dbd6ba9027524537097b2ab84d230a67bcef3f5adf2bb054e502b5806544836f012eb476803ce75d614686af0a4a070794ad1bdd768dc2fa6c26cdc3ac7a7'
            '4ee3df8a34a326c101b831d9ac15469b5ffbe5dbb0ffa0936368d7bcb911c73d0e8bb1dc806992f11e718196ddd69fa6b424b385422c6743fd400b572ea24105')
sha512sums_aarch64=('0c0fab8ccc05bdb14fe0a6b94bc636b1337a2e740681bbe25db263c3b74120e066456fa7607d8ba60aed8336dbf537a4d47e16ddf7e0d1ac009f34e4e66acac3')
sha512sums_x86_64=('f84e01a7d95ff38bd7501257ed5afd80e788533a92f01aae9ebec371c399efa5490b25eb8ebcd4d30a7f90a6fa8b1532bfe4b35278ed5cf88cf5f17bbd73f033')
sha512sums_riscv64=('0516c77bde41233c2006e393b29c0b1323b13dd48cd23c30dfa93d5f27e9dcacb3649f72586401b0dad817a31414cda3013416f65e2fb216adee260042676324')

package() {
  install -Dm755 "${srcdir}/${_pkgname}" "${pkgdir}/usr/bin/${_pkgname}"
  install -Dm644 "${srcdir}/${_pkgname}.service" "${pkgdir}/usr/lib/systemd/system/${_pkgname}.service"
  install -Dm644 "${srcdir}/${_pkgname}-sysusers.conf" "${pkgdir}/usr/lib/sysusers.d/${_pkgname}.conf"
  install -Dm644 "${srcdir}/${_pkgname}-tmpfiles.conf" "${pkgdir}/usr/lib/tmpfiles.d/${_pkgname}.conf"
}
