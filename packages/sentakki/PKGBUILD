# Maintainer: Puqns67 <me@puqns67.icu>

pkgname='sentakki'
pkgver='2025.418.0'
pkgrel=1
pkgdesc='An attempt to recreate maimai gameplay within osu!lazer'
arch=('any')
url='https://github.com/LumpBloom7/sentakki'
license=('MIT')
depends=('osu')
makedepends=('dotnet-sdk')
options=('!debug')

source=("${pkgname}-${pkgver}.tar.gz"::"${url}/archive/refs/tags/${pkgver}.tar.gz")

b2sums=('f638d63d8a3d63c595d27b55e79f79ed5195e09eabd62a7157cbf5b45cee7631f3b90b67b432ba9ce0980154f34c7f6e8a06a0071c8da5a8dea34c337d092eaf')

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	dotnet restore osu.Game.Rulesets.Sentakki
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	dotnet \
    build \
		--framework net8.0 \
		--configuration Release \
		--no-restore \
		--output "${srcdir}/build" \
		osu.Game.Rulesets.Sentakki
}

package() {
  install -vDt "${pkgdir}/usr/lib/osu" -m644 $srcdir/build/osu.Game.Rulesets.Sentakki.*

  # Install license files
  install -vDm644 "${srcdir}/${pkgname}-${pkgver}/LICENSE.md" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.md"
}
