# Maintainer: Puqns67 <me@puqns67.icu>

pkgname='sentakki'
pkgver=2025.605.0
pkgrel=2
pkgdesc='An attempt to recreate maimai gameplay within osu!lazer'
arch=('any')
url='https://github.com/LumpBloom7/sentakki'
license=('MIT')
depends=('osu')
makedepends=('dotnet-sdk')
options=('!debug')

source=("${pkgname}-${pkgver}.tar.gz"::"${url}/archive/refs/tags/${pkgver}.tar.gz")

b2sums=('a0e7ff83fe0c2018815e06f54db3c1383301bbe086abae554bc5c01e00979cdbd71b6e66d1b33f3fa1d8852c7bd0b1a1e64b56c1ce88043c3df31c39e4dab0d0')

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	dotnet restore osu.Game.Rulesets.Sentakki
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	dotnet \
    build \
		--framework net8.0 \
		--configuration Release \
		--no-restore \
		--output "${srcdir}/build" \
		osu.Game.Rulesets.Sentakki
}

package() {
  install -vDt "${pkgdir}/usr/lib/osu" -m644 $srcdir/build/osu.Game.Rulesets.Sentakki.*

  # Install license files
  install -vDm644 "${srcdir}/${pkgname}-${pkgver}/LICENSE.md" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.md"
}
