# Maintainer: Puqns67 <me@puqns67.icu>
# Contributor: David Husička <contact@bydave.net>
# Contributor: Tyler Nelson <neo@cybercat.cc>
# Contributor: Tuhana GAYRETLİ <tuhana at tuta dot io>
# Contributor: Quenten Schoonderwoerd <ellie at nicecock dot eu>
# Contributor: Antti <antti@antti.codes>

# This PKGBUILD is base on the AUR repo osu-lazer-bin, from these contributors above

_pkgname='osu-lazer'
pkgname="${_pkgname}-bin"
pkgver=2024.312.1
pkgrel=3
pkgdesc="The future of osu! and the beginning of an open era! Commonly known by the codename osu!lazer. Pew pew."
arch=('x86_64')
url='https://osu.ppy.sh/'
license=('MIT' 'CC-BY-NC-4.0')
depends=('bash' 'gcc-libs' 'glibc' 'zlib')
provides=("${_pkgname}")
conflicts=("${_pkgname}")

source=("${_pkgname}-${pkgver}.AppImage"::"https://github.com/ppy/osu/releases/download/${pkgver}/osu.AppImage"
        'osu-resources_LICENCE.md'::'https://github.com/ppy/osu-resources/raw/master/LICENCE.md'
        'osu-lazer.bash'
        'osu-lazer.desktop'
        'osu-lazer.xml')

b2sums=('b920870e64bfc11f6ec8202c27e4c5a44477c11a399f0781992a174f898914e35b9ac0124d2b1ae57de9e4ceae200613aba77f64e6210cd5bdb711eeb414fc90'
        '7b7d10932a3c9b07ae10870c7830cacb2dd7164ec3e13ca419a931d6702c496b3ca2b7430f52aa3e71727b8d5f089f174ab13b1c69698925b2e37f8634e3fbff'
        '3d377020b6ac00d2a25ff3a9971b66791593d541a8cdca8e5eaeb815804dd6e4528eea8aa5969dc111e6a7e6940277c3a8f12b170be2a7669ffceebabbd6c0d6'
        '7730cd8b5d5d7fe362e5ccd3023ed4bedf099e851b41db0c75f6a6908375989c3f69900cdb80df2e42031fe9111bf52c4c9fb011a144e5738494996894474428'
        '5ba7f0bcfa2b96b638a19490af619511cea834aa0dfded0efd3262a244bd168fd3362ff3e92a62c6b1a97165b82d52cbf7a11076104fe374c292cc3879e8cb13')

prepare() {
  # Extract AppImage
  cd "${srcdir}"
  "${srcdir}/${_pkgname}-${pkgver}.AppImage" --appimage-extract
}

package() {
  # Install osu-lazer and libraries
  install -dm755 "${pkgdir}/usr/lib"
  mv -v "${srcdir}/squashfs-root/usr/bin" "${pkgdir}/usr/lib/${_pkgname}"

  # Install launch script
  install -Dm755 "${srcdir}/osu-lazer.bash" "${pkgdir}/usr/bin/${_pkgname}"

  # Install license file
  install -Dm644 "${srcdir}/osu-resources_LICENCE.md" "${pkgdir}/usr/share/licenses/${pkgname}/osu-resources_LICENCE.md"

  # Install icon, desktop and mime files
  install -Dm644 "${srcdir}/squashfs-root/osu!.png" "${pkgdir}/usr/share/pixmaps/${_pkgname}.png"
  install -Dm644 "${srcdir}/osu-lazer.desktop" "${pkgdir}/usr/share/applications/${_pkgname}.desktop"
  install -Dm644 "${srcdir}/osu-lazer.xml" "${pkgdir}/usr/share/mime/packages/${_pkgname}.xml"
}
