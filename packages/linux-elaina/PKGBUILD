# Maintainer: Puqns67 <me@puqns67.icu>

_major=6
_minor=11
_patch=2
_xanmod=1
_elaina=1

_xanmod_branch='main'

_to_minor="${_major}.${_minor}"
_to_patch="${_to_minor}.${_patch}"
_to_xanmod="${_to_patch}_xanmod${_xanmod}"

_name='Elaina'
_srcname="linux-${_to_minor}"

pkgbase='linux-elaina'
pkgver="${_to_xanmod}"
pkgrel="${_elaina}"
pkgdesc='Linux kernel for Elaina, based on Xanmod stable and edited some config'
arch=('x86_64')
url='https://www.xanmod.org/'
license=('GPL-2.0-only')
makedepends=('bc' 'cpio' 'gettext' 'libelf' 'pahole' 'perl' 'python' 'tar' 'xz')
options=('!debug' '!strip')

source=("${_srcname}.tar.xz"::"https://cdn.kernel.org/pub/linux/kernel/v${_major}.x/${_srcname}.tar.xz"
        "${_srcname}.tar.sign"::"https://cdn.kernel.org/pub/linux/kernel/v${_major}.x/${_srcname}.tar.sign"
        "xanmod-patch-${_to_xanmod}.xz"::"https://sourceforge.net/projects/xanmod/files/releases/${_xanmod_branch}/${_to_xanmod//_/-}/patch-${_to_xanmod//_/-}.xz/download"
        'choose-gcc-optimization.bash'
        'config.bash'
        'elaina.bash')

sha512sums=('329c1f94008742e3f0c2ce7e591a16316d1b2cb9ea4596d4f45604097e07b7aa2f64afa40630a07f321a858455c77aa32ba57b271932ddcf4dc27863f9081cea'
            'SKIP'
            '5e15495e8b525dd85b75fe207197b0fec2f42ba7a8e3eb2b56cfbe94ae71908e22eca34925149f2f3c15d1df55078aae0b94956e76ad9e80c32c91b7304cfb94'
            '225017227219809ec19458f3c4a9def7d22c2a6cead53f3b10d7e8e6722ec49ab5905576bffd0c50784dee99648815f5bbc65146c5d2d7bcd82b72b52f498aa7'
            'd9130cfa01319836bb5d506a7a9abc818f49b988705eb2697c36424182fd52c2caa868052c43be0d2aff211d99128c45b199f4f2531e8772a28746d7de3f3dc3'
            'd69ab6d45fda910e4c427b5c7824d5076fc8a83434d11ca36d188f0ba5b7aedab7712e10a2e84ff0ae418d933b540da0432d79fb6224661f3d972450ed5aca15')
b2sums=('e7750c0878d71a56a0ce52d4c4c912199dad5bf5e2e8f872585a6494afbb37cbd852e612a6858936d2dc9b7776a3933818f540db408d57e90d18ea5249bba7ab'
        'SKIP'
        'e7457ef5b784d4cf31896bcb01cf12a9b615fea7e046568e6fda9cebaedbde29020cb3eb0a5b03e001c14e06d51b7b6c568c4e348edb0f64c7ff56eb06f0de44'
        '72b9fa70c5e3ce27ecc6a5790cd76c0a5b646248e43e990d1df2e43215adfb9d9fb8cce01adef700e39bef8300af367fc0381ab5d0c774f014351d333a11a5db'
        '32187bdf99e86a7758b65110cd20830d65937dab09c0d32982b6d0c8bfabdf0aa453730e17042590f421d608687b38ed230100ad880d53e2833e54199321455b'
        '1dbaaab7cbaeb2be86abae4a245315571e5c0d4ac45ff236677a725432c92f0cedb9e9de906c5868820c77c2dbd573ae8b2ce5a45f553d008f8802f209fd9e4c')

validpgpkeys=('ABAF11C65A2970B130ABE3C479BE3E4300411886'  # Linux Torvalds
              '647F28654894E3BD457199BE38DBBDC86092693E') # Greg Kroah-Hartman

export KBUILD_BUILD_HOST='archlinux'
export KBUILD_BUILD_USER="${pkgbase}"
export KBUILD_BUILD_TIMESTAMP="$(date -Ru${SOURCE_DATE_EPOCH:+d @$SOURCE_DATE_EPOCH})"

prepare() {
  cd $_srcname

  msg 'Setting version...'
  echo "-${pkgbase#linux-}${pkgrel}" > localversion.10-pkgbuild

  msg 'Applying xanmod patch..'
  patch -Np1 -i "${srcdir}/xanmod-patch-${_to_xanmod}"

  msg 'Applying config for elaina kernel...'
  source "${srcdir}/elaina.bash"

  msg 'make olddefconfig'
  make olddefconfig

  make -s kernelrelease > version
  msg "Prepared %s version %s" "${pkgbase}" "$(<version)"

  # Save configuration for later reuse
  cat .config > "${SRCDEST}/config.last"
}

build() {
  cd $_srcname
  make all
  make -C tools/bpf/bpftool vmlinux.h feature-clang-bpf-co-re=1
}

_package() {
  pkgdesc="The $_name kernel and modules"
  depends=('coreutils' 'initramfs' 'kmod')
  optdepends=('linux-firmware: firmware images needed for some devices')
  provides=('VIRTUALBOX-GUEST-MODULES' 'WIREGUARD-MODULE')

  cd $_srcname
  local modulesdir="$pkgdir/usr/lib/modules/$(<version)"

  msg 'Installing boot image...'
  # systemd expects to find the kernel here to allow hibernation
  # https://github.com/systemd/systemd/commit/edda44605f06a41fb86b7ab8128dcf99161d2344
  install -Dm644 "$(make -s image_name)" "$modulesdir/vmlinuz"

  # Used by mkinitcpio to name the kernel
  echo "$pkgbase" | install -Dm644 /dev/stdin "$modulesdir/pkgbase"

  msg 'Installing modules...'
  ZSTD_CLEVEL=19 make INSTALL_MOD_PATH="$pkgdir/usr" INSTALL_MOD_STRIP=1 \
    DEPMOD=/doesnt/exist modules_install  # Suppress depmod

  # remove build link
  rm "$modulesdir"/build
}

_package-headers() {
  pkgdesc="Headers and scripts for building modules for the $_name kernel"
  depends=('pahole')

  cd $_srcname
  local builddir="$pkgdir/usr/lib/modules/$(<version)/build"

  msg 'Installing build files...'
  install -Dt "$builddir" -m644 .config Makefile Module.symvers System.map \
    localversion.* version vmlinux tools/bpf/bpftool/vmlinux.h
  install -Dt "$builddir/kernel" -m644 kernel/Makefile
  install -Dt "$builddir/arch/x86" -m644 arch/x86/Makefile
  cp -t "$builddir" -a scripts

  # required when STACK_VALIDATION is enabled
  install -Dt "$builddir/tools/objtool" tools/objtool/objtool

  # required when DEBUG_INFO_BTF_MODULES is enabled
  install -Dt "$builddir/tools/bpf/resolve_btfids" tools/bpf/resolve_btfids/resolve_btfids

  msg 'Installing headers...'
  cp -t "$builddir" -a include
  cp -t "$builddir/arch/x86" -a arch/x86/include
  install -Dt "$builddir/arch/x86/kernel" -m644 arch/x86/kernel/asm-offsets.s

  install -Dt "$builddir/drivers/md" -m644 drivers/md/*.h
  install -Dt "$builddir/net/mac80211" -m644 net/mac80211/*.h

  # https://bugs.archlinux.org/task/13146
  install -Dt "$builddir/drivers/media/i2c" -m644 drivers/media/i2c/msp3400-driver.h

  # https://bugs.archlinux.org/task/20402
  install -Dt "$builddir/drivers/media/usb/dvb-usb" -m644 drivers/media/usb/dvb-usb/*.h
  install -Dt "$builddir/drivers/media/dvb-frontends" -m644 drivers/media/dvb-frontends/*.h
  install -Dt "$builddir/drivers/media/tuners" -m644 drivers/media/tuners/*.h

  # https://bugs.archlinux.org/task/71392
  install -Dt "$builddir/drivers/iio/common/hid-sensors" -m644 drivers/iio/common/hid-sensors/*.h

  msg 'Installing KConfig files...'
  find . -name 'Kconfig*' -exec install -Dm644 {} "$builddir/{}" \;

  msg 'Removing unneeded architectures...'
  local arch
  for arch in "$builddir"/arch/*/; do
    [[ $arch = */x86/ ]] && continue
    msg2 'Removing %s' $(basename $arch)
    rm -r "$arch"
  done

  msg 'Removing documentation...'
  rm -r "$builddir/Documentation"

  msg 'Removing broken symlinks...'
  find -L "$builddir" -type l -printf 'Removing %P\n' -delete

  msg 'Removing loose objects...'
  find "$builddir" -type f -name '*.o' -printf 'Removing %P\n' -delete

  msg 'Stripping build tools...'
  local file
  while read -rd '' file; do
    case "$(file -Sib "$file")" in
      application/x-sharedlib\;*)      # Libraries (.so)
        strip -v $STRIP_SHARED "$file" ;;
      application/x-archive\;*)        # Libraries (.a)
        strip -v $STRIP_STATIC "$file" ;;
      application/x-executable\;*)     # Binaries
        strip -v $STRIP_BINARIES "$file" ;;
      application/x-pie-executable\;*) # Relocatable binaries
        strip -v $STRIP_SHARED "$file" ;;
    esac
  done < <(find "$builddir" -type f -perm -u+x ! -name vmlinux -print0)

  msg 'Stripping vmlinux...'
  strip -v $STRIP_STATIC "$builddir/vmlinux"

  msg 'Adding symlink...'
  mkdir -p "$pkgdir/usr/src"
  ln -sr "$builddir" "$pkgdir/usr/src/$pkgbase"
}

pkgname=("${pkgbase}" "${pkgbase}-headers")
for _p in "${pkgname[@]}"; do
  eval "package_$_p() {
    $(declare -f "_package${_p#$pkgbase}")
    _package${_p#$pkgbase}
  }"
done

# vim:set ts=8 sts=2 sw=2 et:
