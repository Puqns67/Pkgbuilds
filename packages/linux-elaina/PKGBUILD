# Maintainer: Puqns67 <me@puqns67.icu>

_major=6
_minor=15
_patch=2
_xanmod=1
_elaina=1

_xanmod_branch='edge'

_to_minor="${_major}.${_minor}"
_to_patch="${_to_minor}.${_patch}"
_to_xanmod="${_to_patch}_xanmod${_xanmod}"

_name='Elaina'
_srcname="linux-${_to_minor}"

pkgbase='linux-elaina'
pkgver="${_to_xanmod}"
pkgrel="${_elaina}"
pkgdesc='Linux kernel for Elaina, based on Xanmod stable and edited some config'
arch=('x86_64')
url='https://www.xanmod.org/'
license=('GPL-2.0-only')
makedepends=('bc' 'clang' 'cpio' 'lld' 'llvm' 'pahole' 'python')
options=('!debug' '!strip')

source=("${_srcname}.tar.xz"::"https://cdn.kernel.org/pub/linux/kernel/v${_major}.x/${_srcname}.tar.xz"
        "${_srcname}.tar.sign"::"https://cdn.kernel.org/pub/linux/kernel/v${_major}.x/${_srcname}.tar.sign"
        "xanmod-patch-${_to_xanmod}.xz"::"https://sourceforge.net/projects/xanmod/files/releases/${_xanmod_branch}/${_to_xanmod//_/-}/patch-${_to_xanmod//_/-}.xz/download"
        'config.bash'
        'elaina.bash')

sha512sums=('d03788ffa8d8ae1b84ef1286bb44a08fc01432e509dfec6cccae5c5a5a47201d378aec2bcd21e6f0bbd1e625b26f47780c06ee9c1cef3775358f98b160923f30'
            'SKIP'
            '648d016c7537abb517fabb05e4f928fb40fc07c8b945a99f03f5fed0e61f5db5176f85faaf416440304cb3b5c5458ab85cf46544cf291b4a79dc95ef982a2530'
            '2dbfce9ec1be85308f2760f930318a8aae290f1cab129faa71271d433707a7cc192bd984fdb2a0c7374d845056a0390a76212427375f6883d61c0fdb94f6d513'
            '61ad3bf8f1b8af96a408c0970e963f8624a16c8462cdc301905e4b65355d2a58c22645e6839c59fe6d61f3fb0c880d67324f4463e71605332c3839b6e62d4d03')
b2sums=('465596c6dc053ff3a3966302a906d3edb4f7ee1ef82f8c20b96360196d3414f5b1deeafa67b8340fcdecd3617280ba9b756d7073ad15c707865e256397b4af53'
        'SKIP'
        '3aa3396bee4c16b2e2e7f1ea0c77f524a5a8aee4a082e342980bcea7c6c153194d24df47f5822ad9e5d80a218b15feeb1cb7509158a72b84ff1a6e17190703a2'
        'af30ef0927a4de2556041472796f85d99bd43d63b1507ccf17e566b642d9278cbfa0c705f40500a04c2f3d0e0af2c24d885309acc7d2481e7e4537a2021bf20f'
        '291a693bbfe70ba845546a1874e0c73bca45c507f06982e2df9d63ae59846d185573b326844f43de6e66b19042f291ea87c39644dd4bb95ff7bf486bbf475aaa')

validpgpkeys=('ABAF11C65A2970B130ABE3C479BE3E4300411886'  # Linux Torvalds
              '647F28654894E3BD457199BE38DBBDC86092693E') # Greg Kroah-Hartman

export KBUILD_BUILD_HOST='archlinux'
export KBUILD_BUILD_USER="${pkgbase}"
export KBUILD_BUILD_TIMESTAMP="$(date -Ru${SOURCE_DATE_EPOCH:+d @$SOURCE_DATE_EPOCH})"
export LLVM=1

prepare() {
  cd $_srcname

  msg 'Setting version...'
  echo "-${pkgbase#linux-}${pkgrel}" > localversion.10-pkgbuild

  msg 'Applying xanmod patch..'
  patch -Np1 -i "${srcdir}/xanmod-patch-${_to_xanmod}"

  msg 'Applying config for elaina kernel...'
  source "${srcdir}/elaina.bash"

  msg 'make olddefconfig'
  make -j olddefconfig

  make -s kernelrelease > version
  msg "Prepared %s version %s" "${pkgbase}" "$(<version)"

  # Save configuration for later reuse
  cat .config > "${SRCDEST}/config.last"
}

build() {
  cd $_srcname
  make KCFLAGS="-Wno-error=incompatible-pointer-types-discards-qualifiers" all
  make -C tools/bpf/bpftool vmlinux.h feature-clang-bpf-co-re=1
}

_package() {
  pkgdesc="The $_name kernel and modules"
  depends=('coreutils' 'initramfs' 'kmod')
  optdepends=('linux-firmware: firmware images needed for some devices')
  provides=('VIRTUALBOX-GUEST-MODULES' 'WIREGUARD-MODULE')

  cd $_srcname
  local modulesdir="$pkgdir/usr/lib/modules/$(<version)"

  msg 'Installing boot image...'
  # systemd expects to find the kernel here to allow hibernation
  # https://github.com/systemd/systemd/commit/edda44605f06a41fb86b7ab8128dcf99161d2344
  install -Dm644 "$(make -s image_name)" "$modulesdir/vmlinuz"

  # Used by mkinitcpio to name the kernel
  echo "$pkgbase" | install -Dm644 /dev/stdin "$modulesdir/pkgbase"

  msg 'Installing modules...'
  ZSTD_CLEVEL=19 make INSTALL_MOD_PATH="$pkgdir/usr" INSTALL_MOD_STRIP=1 \
    DEPMOD=/doesnt/exist modules_install  # Suppress depmod

  # remove build link
  rm "$modulesdir"/build
}

_package-headers() {
  pkgdesc="Headers and scripts for building modules for the $_name kernel"
  depends=('pahole')

  cd $_srcname
  local builddir="$pkgdir/usr/lib/modules/$(<version)/build"

  msg 'Installing build files...'
  install -Dt "$builddir" -m644 .config Makefile Module.symvers System.map \
    localversion.* version vmlinux tools/bpf/bpftool/vmlinux.h
  install -Dt "$builddir/kernel" -m644 kernel/Makefile
  install -Dt "$builddir/arch/x86" -m644 arch/x86/Makefile
  cp -t "$builddir" -a scripts

  # required when STACK_VALIDATION is enabled
  install -Dt "$builddir/tools/objtool" tools/objtool/objtool

  # required when DEBUG_INFO_BTF_MODULES is enabled
  install -Dt "$builddir/tools/bpf/resolve_btfids" tools/bpf/resolve_btfids/resolve_btfids

  msg 'Installing headers...'
  cp -t "$builddir" -a include
  cp -t "$builddir/arch/x86" -a arch/x86/include
  install -Dt "$builddir/arch/x86/kernel" -m644 arch/x86/kernel/asm-offsets.s

  install -Dt "$builddir/drivers/md" -m644 drivers/md/*.h
  install -Dt "$builddir/net/mac80211" -m644 net/mac80211/*.h

  # https://bugs.archlinux.org/task/13146
  install -Dt "$builddir/drivers/media/i2c" -m644 drivers/media/i2c/msp3400-driver.h

  # https://bugs.archlinux.org/task/20402
  install -Dt "$builddir/drivers/media/usb/dvb-usb" -m644 drivers/media/usb/dvb-usb/*.h
  install -Dt "$builddir/drivers/media/dvb-frontends" -m644 drivers/media/dvb-frontends/*.h
  install -Dt "$builddir/drivers/media/tuners" -m644 drivers/media/tuners/*.h

  # https://bugs.archlinux.org/task/71392
  install -Dt "$builddir/drivers/iio/common/hid-sensors" -m644 drivers/iio/common/hid-sensors/*.h

  msg 'Installing KConfig files...'
  find . -name 'Kconfig*' -exec install -Dm644 {} "$builddir/{}" \;

  msg 'Removing unneeded architectures...'
  local arch
  for arch in "$builddir"/arch/*/; do
    [[ $arch = */x86/ ]] && continue
    msg2 'Removing %s' $(basename $arch)
    rm -r "$arch"
  done

  msg 'Removing documentation...'
  rm -r "$builddir/Documentation"

  msg 'Removing broken symlinks...'
  find -L "$builddir" -type l -printf 'Removing %P\n' -delete

  msg 'Removing loose objects...'
  find "$builddir" -type f -name '*.o' -printf 'Removing %P\n' -delete

  msg 'Stripping build tools...'
  local file
  while read -rd '' file; do
    case "$(file -Sib "$file")" in
      application/x-sharedlib\;*)      # Libraries (.so)
        strip -v $STRIP_SHARED "$file" ;;
      application/x-archive\;*)        # Libraries (.a)
        strip -v $STRIP_STATIC "$file" ;;
      application/x-executable\;*)     # Binaries
        strip -v $STRIP_BINARIES "$file" ;;
      application/x-pie-executable\;*) # Relocatable binaries
        strip -v $STRIP_SHARED "$file" ;;
    esac
  done < <(find "$builddir" -type f -perm -u+x ! -name vmlinux -print0)

  msg 'Stripping vmlinux...'
  strip -v $STRIP_STATIC "$builddir/vmlinux"

  msg 'Adding symlink...'
  mkdir -p "$pkgdir/usr/src"
  ln -sr "$builddir" "$pkgdir/usr/src/$pkgbase"
}

pkgname=("${pkgbase}" "${pkgbase}-headers")
for _p in "${pkgname[@]}"; do
  eval "package_$_p() {
    $(declare -f "_package${_p#$pkgbase}")
    _package${_p#$pkgbase}
  }"
done
