# Maintainer: Puqns67 <me@puqns67.icu>

pkgname='tuwunel'
pkgver=1.4.4.0
pkgrel=1
pkgdesc='A featureful Matrix homeserver'
arch=('x86_64')
url='https://github.com/matrix-construct/tuwunel'
license=('Apache-2.0')
depends=('gcc-libs' 'glibc' 'liburing')
makedepends=('cargo' 'clang' 'lld' 'llvm')
backup=("etc/${pkgname}/config.toml")
options=('!lto')

source=("${pkgname}-${pkgver}.tar.gz"::"${url}/archive/refs/tags/v${pkgver}.tar.gz"
        "${pkgname}.service"
        "${pkgname}-sysusers.conf"
        "${pkgname}-tmpfiles.conf")

b2sums=('e1b56fe407d0569626d1a2f60ab59bd722d058c8726e77d4c24d40b5cf148de477286bddc5e75d53f887af982b0fe063f64099153fd6b43f4817b94bcb99cf12'
        '98a3ce7c2274b95cff97eae54a408c0804585b530e69f6e15d60bb91ab854cd30068f127578cbeaa3f906496d5600076a6372609c165a4b475a57a8e04c52062'
        'e6aeae38f93c078471742ffc25dea2c4bcd5aeda8cc184f33eca0db53019367ae6da2522f703ee346ef83a73d99a1703eea6cbe71393ca91a5678b781ba03c18'
        '443daebba5262947ab50dcb14e388dfb4f1fa1a0cbd4ee6987f75a53dfbaabcea230596573fc3bac7e50bfe7bd3468ffc1e901bd8d9234b4d70beb3a82324f10')

prepare() {
  export RUSTUP_TOOLCHAIN=stable
  cd "${srcdir}/${pkgname}-${pkgver}"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  export CC=clang
  export CXX=clang++
  export AR=llvm-ar
  export NM=llvm-nm
  export RANLIB=llvm-ranlib
  export LDFLAGS="-fuse-ld=lld"
  cd "${srcdir}/${pkgname}-${pkgver}"
  cargo build --frozen --release --all-features
}

package() {
  install -Dm755 "${srcdir}/${pkgname}-${pkgver}/target/release/${pkgname}" "${pkgdir}/usr/bin/${pkgname}"
  install -Dm644 "${srcdir}/${pkgname}-${pkgver}/${pkgname}-example.toml" "${pkgdir}/etc/${pkgname}/config.toml"
  install -Dm644 "${srcdir}/${pkgname}.service" "${pkgdir}/usr/lib/systemd/system/${pkgname}.service"
  install -Dm644 "${srcdir}/${pkgname}-sysusers.conf" "${pkgdir}/usr/lib/sysusers.d/${pkgname}.conf"
  install -Dm644 "${srcdir}/${pkgname}-tmpfiles.conf" "${pkgdir}/usr/lib/tmpfiles.d/${pkgname}.conf"
}
