# Maintainer: Puqns67 <me@puqns67.icu>

_pkgname='PrismLauncher'

pkgname='prismlauncher'
pkgver=8.4
pkgrel=1
pkgdesc='Minecraft launcher with ability to manage multiple instances.'
arch=('x86_64' 'aarch64')
url='https://prismlauncher.org/'
license=('GPL-3.0-only')
depends=('bzip2' 'cmark' 'gcc-libs' 'glibc' 'hicolor-icon-theme' 'libglvnd' 'qt6-5compat' 'qt6-base' 'quazip-qt6' 'tomlplusplus' 'zlib')
makedepends=('cmake' 'extra-cmake-modules' 'gamemode' 'ghc-filesystem' 'java-environment=17' 'ninja' 'scdoc')
optdepends=('flite: for Minecraft text2speech support'
            'glfw: to use system GLFW libraries'
            'openal: to use system OpenAL libraries'
            'visualvm: Profiling support'
            'mangohud: run Minecraft with mangohud'
            'gamemode: run Minecraft with gamemode'
            'xorg-xrandr: for older Minecraft (<=1.12.2)'
            'java-runtime=8: for older Minecraft (<1.18 Pre-release 2)'
            'java-runtime=17: for Minecraft 1.18.0+ (>=1.18 Pre-release 2)'
            'java-runtime=21: for Minecraft 1.20.5+ (>=24w14a)'
            'qt6-wayland: for Wayland support')

source=("${pkgname}-${pkgver}.tar.gz"::"https://github.com/PrismLauncher/PrismLauncher/releases/download/${pkgver}/${_pkgname}-${pkgver}.tar.gz")

b2sums=('c7950df3d843e370914e2a72c71847a71ab0446508a464ce04ded007b0b3170e8119bb30455d1022fda98cd49f16528a63f5589766561cfc15950c6823779b6f')

prepare() {
  cmake \
    -B "${srcdir}/build" \
    -D CMAKE_BUILD_TYPE=RelWithDebInfo \
    -D CMAKE_INSTALL_PREFIX=/usr \
    -D BUILD_TESTING=OFF \
    -D Launcher_BUILD_PLATFORM=archlinux \
    -G Ninja \
    -S "${srcdir}/${_pkgname}-${pkgver}"
}

build() {
  cmake --build "${srcdir}/build"
}

package() {
  DESTDIR="${pkgdir}" cmake --install "${srcdir}/build"
}
