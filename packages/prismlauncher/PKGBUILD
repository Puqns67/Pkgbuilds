# Maintainer: Puqns67 <me@puqns67.icu>

_pkgname='PrismLauncher'

pkgname='prismlauncher'
pkgver=10.0.1
pkgrel=1
pkgdesc='Minecraft launcher with ability to manage multiple instances'
arch=('x86_64' 'aarch64')
url='https://prismlauncher.org/'
license=('GPL-3.0-only AND LGPL-3.0-or-later AND LGPL-2.0-or-later AND Apache-2.0 AND MIT AND LicenseRef-Batch AND OFL-1.1')
depends=('cmark' 'gcc-libs' 'glibc' 'hicolor-icon-theme' 'libarchive' 'qrencode' 'qt6-base' 'qt6-networkauth' 'tomlplusplus' 'zlib')
makedepends=('cmake' 'extra-cmake-modules' 'gamemode' 'java-environment=17')
optdepends=('glfw: to use system GLFW libraries'
            'openal: to use system OpenAL libraries'
            'visualvm: Profiling support'
            'xorg-xrandr: for older minecraft versions (<=1.12.2)'
            'flite: minecraft voice narration'
            'mangohud: run Minecraft with mangohud'
            'gamemode: run Minecraft with gamemode'
            'java-runtime=8: for older Minecraft (<1.18 Pre-release 2)'
            'java-runtime=17: for Minecraft 1.18.0+ (>=1.18 Pre-release 2)'
            'java-runtime=21: for Minecraft 1.20.5+ (>=24w14a)')

source=("${pkgname}-${pkgver}.tar.gz"::"https://github.com/PrismLauncher/PrismLauncher/releases/download/${pkgver}/${_pkgname}-${pkgver}.tar.gz"
        LICENSE-{batch,lionshead,mdi})

b2sums=('684739c99aed623fade5b0e84e2256e6e9e8bd0ea10fc3c0bdefe6067060d72addf82d5e796c4513bdded10846e1b6d9ef28fb2ac1640f9c22b2b37eeff55f00'
        '356248a6b86f06d260e0920b49d34034f79f9bc504c7fdc1849d929d2ff9b169e693a8269a2c0b34656b3802970d9b8be41a92b35177eaa3c4ccc89a702f5c9d'
        'be4289832af95b1cd6e721dc16b84a034533de9718d9b43a49bd08dd6fe4e28eaa15228bfb311867b18fddbda1c9fc4c91f04c6d5c1a3bcc39aaa5161425e3ba'
        'b35c447cd9223e096a2bb75e0741a7d0a3a1606af54c957e4f276f4e6861a9b3f06ae1d646137e8d2f24ba2238c9967c76eff8cc631a68d7e48e376056982cc6')

prepare() {
  cmake \
    -B "${srcdir}/build" \
    -D CMAKE_BUILD_TYPE=RelWithDebInfo \
    -D CMAKE_INSTALL_PREFIX=/usr \
    -D ENABLE_LTO=ON \
    -D Launcher_BUILD_PLATFORM=archlinux \
    -S "${srcdir}/${_pkgname}-${pkgver}"
}

build() {
  cmake --build "${srcdir}/build"
}

check() {
  ctest --test-dir "${srcdir}/build" --output-on-failure
}

package() {
  DESTDIR="${pkgdir}" cmake --install "${srcdir}/build"

  # licenses
  install -Dm644 -t "${pkgdir}/usr/share/licenses/${pkgname}" ${srcdir}/LICENSE-{batch,lionshead,mdi}
}
