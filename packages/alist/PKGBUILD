# Maintainer: Puqns67 <me@puqns67.icu>

_organization_url='https://github.com/alist-org'
_solid_router_git_hash='f77b206e8592bb56d2d001e9488de5a54a13699b'

pkgname='alist'
pkgver=3.28.0
pkgrel=1
pkgdesc='A file list/WebDAV program that supports multiple storages, powered by Gin and Solidjs.'
url="${_organization_url}/${pkgname}"
license=('AGPL3')
arch=('i686' 'x86_64' 'armv7h' 'aarch64' 'riscv64')
depends=('glibc')
makedepends=('go' 'pnpm')

source=("${pkgname}-backend-${pkgver}.tar.gz"::"${url}/archive/refs/tags/v${pkgver}.tar.gz"
        "${pkgname}-frontend-${pkgver}.tar.gz"::"${_organization_url}/${pkgname}-web/archive/refs/tags/${pkgver}.tar.gz"
        "${pkgname}-solid_router-${_solid_router_git_hash}.tar.gz"::"${_organization_url}/solid-router/archive/${_solid_router_git_hash}.tar.gz"
        "${pkgname}.service"
        "${pkgname}-sysusers.conf"
        "${pkgname}-tmpfiles.conf")

sha512sums=('e582174a499d96240bd0a5672aeb12dfd36cfb4484343fdea4cc3cd44318b3334681e28ddea3a66e66a64394d95e01b2b588fc932bfb1094b2cf3b35dbc44f49'
            '3748542a50a60eb0247c6be82911042c241fa62507c5a5df52fe7e44cc316ebc8a09e236ba8625a879ce62311db55fc6ad01eddbc703d87516264199a8bbe012'
            '0638e98b8f2866e7cbdd5420c6b31558aeeea05e521dd28c22b917da4c419df933f03d042ceba0a88b7a5a153e152b7abc32af1acc0b90e8be90c3ddbdd4f541'
            'e8bfd6079626799ca7af015550c5605991631460d18be2aab41d58412488fd17672b9125c47cfb019d519d883d6b89e4f2f367eddfb30d5857e29fd91f582f78'
            '923dbd6ba9027524537097b2ab84d230a67bcef3f5adf2bb054e502b5806544836f012eb476803ce75d614686af0a4a070794ad1bdd768dc2fa6c26cdc3ac7a7'
            '4ee3df8a34a326c101b831d9ac15469b5ffbe5dbb0ffa0936368d7bcb911c73d0e8bb1dc806992f11e718196ddd69fa6b424b385422c6743fd400b572ea24105')

prepare() {
  # Link frontend dist to backend sources.
  rm -rfv "${srcdir}/${pkgname}-web-${pkgver}/dist"
  ln -sfv "${srcdir}/${pkgname}-${pkgver}/public/dist" "${srcdir}/${pkgname}-web-${pkgver}/dist"

  # Move solid-router to web sources.
  rm -rfv "${srcdir}/${pkgname}-web-${pkgver}/solid-router"
  mv -v "${srcdir}/solid-router-${_solid_router_git_hash}" "${srcdir}/${pkgname}-web-${pkgver}/solid-router"

  # Update frontend version with $pkgver.
  sed -ie "s/0.0.0/${pkgver}/g" "${srcdir}/${pkgname}-web-${pkgver}/package.json"
}

build() {
  # Build frontend
  cd "${srcdir}/${pkgname}-web-${pkgver}"
  HUSKY=0 pnpm install
  pnpm build

  # Build backend
  cd "${srcdir}/${pkgname}-${pkgver}"
  GOFLAGS="-trimpath -modcacherw" go build -o "${srcdir}/${pkgname}" \
    -ldflags="-w -s -linkmode=external \
    -X 'github.com/alist-org/alist/v3/internal/conf.BuiltAt=$(date +"%F %T %z")' \
    -X 'github.com/alist-org/alist/v3/internal/conf.GoVersion=$(go version | sed "s/go version //")' \
    -X 'github.com/alist-org/alist/v3/internal/conf.Version=${pkgver}' \
    -X 'github.com/alist-org/alist/v3/internal/conf.WebVersion=${pkgver}'"
}

package() {
  install -Dm755 "${srcdir}/${pkgname}" "${pkgdir}/usr/bin/${pkgname}"
  install -Dm644 "${srcdir}/${pkgname}.service" "${pkgdir}/usr/lib/systemd/system/${pkgname}.service"
  install -Dm644 "${srcdir}/${pkgname}-sysusers.conf" "${pkgdir}/usr/lib/sysusers.d/${pkgname}.conf"
  install -Dm644 "${srcdir}/${pkgname}-tmpfiles.conf" "${pkgdir}/usr/lib/tmpfiles.d/${pkgname}.conf"
}
