# Maintainer: Puqns67 <me@puqns67.icu>

pkgname='alist'
pkgver=3.32.0
pkgrel=1
pkgdesc='A file list/WebDAV program that supports multiple storages, powered by Gin and Solidjs.'
url="https://github.com/alist-org/alist"
license=('AGPL-3.0-or-later')
arch=('x86_64' 'aarch64' 'riscv64')
depends=('alist-web' 'glibc')
makedepends=('go')
backup=("etc/${pkgname}/config.json")

source=("${pkgname}-${pkgver}.tar.gz"::"${url}/archive/refs/tags/v${pkgver}.tar.gz"
        "${pkgname}.service"
        "${pkgname}-config.json"
        "${pkgname}-sysusers.conf"
        "${pkgname}-tmpfiles.conf")

b2sums=('ccb550e97401e1c96e6cfbe4ec08bc9c4190fe0c25d6d987d3a870ed70af7d40257f327771647df488b5e6e32fbbb38a802d7881ea0664c7ad2011f72c46d356'
        '31f28bbf375abfd5bd92f5868d4fdb8da013ca0c6fb5c9440f2113058f10521fa9f89b6d7f5463f782c5af5d33710b8ae7f153d460a7bcd294c21b7539d62cd4'
        '9327a492264ecac0806b031b780241d86cabe38348fe49c4c5a610ee584cfbaaefd3fdffd1b1b54c9ee225820433a7f902c688b2e123181a56c73b9cbf9cd13f'
        '3ae4af13c2731c5343b5c000d14725aada399e314c8d1309bb2c668053c385f759351f62e09246e1cfca9fbe40033faee08722d7456ea8c66c9b77c20bff0c0b'
        '9169de3f1f689f5fd7252fc7457678aa3706e992fbe6f18b04c695a2dbb11f99347175e0b86c236faf3fba5c5a42bf1a88b3724f87b79c9cf3e6f31d43c8613f')

prepare() {
  # Allow read dist_dir from environment
  sed -i "s/json:\"dist_dir\"/json:\"dist_dir\" env:\"DIST_DIR\"/g" "${srcdir}/${pkgname}-${pkgver}/internal/conf/config.go"

  # Download dependencies
  cd "${srcdir}/${pkgname}-${pkgver}"
  go mod download -x -modcacherw
}

build() {
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-v -x -work -modcacherw -trimpath -buildmode=pie -mod=readonly"

  cd "${srcdir}/${pkgname}-${pkgver}"
  go build -o "${srcdir}/${pkgname}" \
    -ldflags="-w -s -linkmode external \
      -X 'github.com/alist-org/alist/v3/internal/conf.BuiltAt=$(date +"%F %T %z")' \
      -X 'github.com/alist-org/alist/v3/internal/conf.GoVersion=$(go version | sed "s/go version //")' \
      -X 'github.com/alist-org/alist/v3/internal/conf.Version=${pkgver}'"
}

package() {
  install -Dm755 "${srcdir}/${pkgname}" "${pkgdir}/usr/bin/${pkgname}"
  install -Dm644 "${srcdir}/${pkgname}.service" "${pkgdir}/usr/lib/systemd/system/${pkgname}.service"
  install -Dm644 "${srcdir}/${pkgname}-sysusers.conf" "${pkgdir}/usr/lib/sysusers.d/${pkgname}.conf"
  install -Dm644 "${srcdir}/${pkgname}-tmpfiles.conf" "${pkgdir}/usr/lib/tmpfiles.d/${pkgname}.conf"
  install -Dm644 "${srcdir}/${pkgname}-config.json" "${pkgdir}/etc/${pkgname}/config.json"
}
