# Maintainer: Puqns67 <me@puqns67.icu>

pkgname='alist'
pkgver=3.30.0
pkgrel=2
pkgdesc='A file list/WebDAV program that supports multiple storages, powered by Gin and Solidjs.'
url="https://github.com/alist-org/alist"
license=('AGPL-3.0-or-later')
arch=('x86_64' 'aarch64' 'riscv64')
depends=('alist-web' 'glibc')
makedepends=('go')

source=("${pkgname}-${pkgver}.tar.gz"::"${url}/archive/refs/tags/v${pkgver}.tar.gz"
        "${pkgname}.service"
        "${pkgname}-config.json"
        "${pkgname}-sysusers.conf"
        "${pkgname}-tmpfiles.conf")

sha512sums=('96b5b4aeaa5de9c9807ae13e8b63ee8c95854432b0e399610e2b6cdbdceac166b9102d7698907183a590435794364dced47b1927e34f0010f1eb89a199664f82'
            'e8bfd6079626799ca7af015550c5605991631460d18be2aab41d58412488fd17672b9125c47cfb019d519d883d6b89e4f2f367eddfb30d5857e29fd91f582f78'
            'd7032d1e72e43f5e7ab053117031359c4a0df9be277d5e0224f81cbaa27004fe0c96c6351e9ea9a467aaa92847a9ec966f469d56aee8c5984816bb337f08caea'
            '923dbd6ba9027524537097b2ab84d230a67bcef3f5adf2bb054e502b5806544836f012eb476803ce75d614686af0a4a070794ad1bdd768dc2fa6c26cdc3ac7a7'
            'b642ea910aef67c99a4fdd1474cbf0d8996e1688309aa08026994c26b58a3b14ded273870bc29c3fb7d98c4203e6093ad2bd645a92a08183f4fe4f5232048a1d')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  go build -v -x -work -trimpath -modcacherw -o "${srcdir}/${pkgname}" \
    -ldflags="-w -s \
      -X 'github.com/alist-org/alist/v3/internal/conf.BuiltAt=$(date +"%F %T %z")' \
      -X 'github.com/alist-org/alist/v3/internal/conf.GoVersion=$(go version | sed "s/go version //")' \
      -X 'github.com/alist-org/alist/v3/internal/conf.Version=${pkgver}'"
}

package() {
  install -Dm755 "${srcdir}/${pkgname}" "${pkgdir}/usr/bin/${pkgname}"
  install -Dm644 "${srcdir}/${pkgname}.service" "${pkgdir}/usr/lib/systemd/system/${pkgname}.service"
  install -Dm644 "${srcdir}/${pkgname}-sysusers.conf" "${pkgdir}/usr/lib/sysusers.d/${pkgname}.conf"
  install -Dm644 "${srcdir}/${pkgname}-tmpfiles.conf" "${pkgdir}/usr/lib/tmpfiles.d/${pkgname}.conf"
}
