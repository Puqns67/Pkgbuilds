# Maintainer: Puqns67 <me@puqns67.icu>

_organization_url='https://github.com/alist-org'
_solid_router_git_hash='f77b206e8592bb56d2d001e9488de5a54a13699b'

pkgname='alist-web'
pkgver=3.32.0
pkgrel=1
pkgdesc='A file list/WebDAV program that supports multiple storages, powered by Gin and Solidjs.'
url="${_organization_url}/${pkgname}"
license=('AGPL-3.0-or-later')
arch=('any')
makedepends=('pnpm')

source=("${pkgname}-${pkgver}.tar.gz"::"${url}/archive/refs/tags/${pkgver}.tar.gz"
        "solid_router-${_solid_router_git_hash}.tar.gz"::"${_organization_url}/solid-router/archive/${_solid_router_git_hash}.tar.gz")

b2sums=('d8898895792a77f8802f7a1a83248392ef63a9c50c142c0957a084e5b7d3676fa3e6f7e09373278bf404de80632b70fd533f5739ef33b313dbf6cf318db29a33'
        '0678eafed0908109fda831cbdeccb676e212960bdec0ad062064415566c8f11076bf3a706a1719b21d238dccbaa96e47cb5d265f4d7e5f21f2bd4f5a1e1296f9')

prepare() {
  # Move solid-router to sources.
  rm -rfv "${srcdir}/${pkgname}-${pkgver}/solid-router"
  mv -v "${srcdir}/solid-router-${_solid_router_git_hash}" "${srcdir}/${pkgname}-${pkgver}/solid-router"

  # Update version with $pkgver.
  sed -ie "s/0.0.0/${pkgver}/g" "${srcdir}/${pkgname}-${pkgver}/package.json"

  # Install dependencies
  cd "${srcdir}/${pkgname}-${pkgver}"
  HUSKY=0 pnpm install
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  pnpm build
}

package() {
  install -dm755 "${pkgdir}/usr/share"
  cp -rv "${srcdir}/${pkgname}-${pkgver}/dist" "${pkgdir}/usr/share/${pkgname}"
}
