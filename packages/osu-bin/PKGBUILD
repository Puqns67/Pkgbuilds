# Maintainer: Puqns67 <me@puqns67.icu>
# Contributor: David Husička <contact@bydave.net>
# Contributor: Tyler Nelson <neo@cybercat.cc>
# Contributor: Tuhana GAYRETLİ <tuhana at tuta dot io>
# Contributor: Quenten Schoonderwoerd <ellie at nicecock dot eu>
# Contributor: Antti <antti@antti.codes>

# This PKGBUILD is base on the AUR repo osu-lazer-bin, from these contributors above

_appname='osu'
_appver='2025.118.0'
_resname='osu-resources'
_resver='2024.1224.0'

pkgname="${_appname}-bin"
pkgver="${_appver}"
pkgrel=1
pkgdesc="A free-to-win rhythm game. Rhythm is just a click away!"
arch=('x86_64')
url='https://osu.ppy.sh/'
license=('MIT' 'CC-BY-NC-4.0')
depends=('bash' 'ffmpeg4.4' 'gcc-libs' 'glibc' 'hicolor-icon-theme' 'lttng-ust2.12' 'sdl2' 'zlib')
makedepends=('imagemagick')
optdepends=('gamemode: run osu with gamemode')
provides=("${_appname}" 'osu-lazer')
conflicts=("${_appname}" 'osu-lazer')
replaces=('osu-lazer' 'osu-lazer-bin')
options=('!debug')

source=("${_appname}-${_appver}.AppImage"::"https://github.com/ppy/osu/releases/download/${_appver}/osu.AppImage"
        "${_resname}-LICENCE-${_resver}.md"::"https://github.com/ppy/osu-resources/raw/${_resver}/LICENCE.md"
        "${_appname}.bash"
        "${_appname}.desktop"
        "${_appname}.xml")

b2sums=('7f3d3334b4755186f30c6fb6140db5585d0c83df641a53650edede8c4b08973b4cf6b4a0b9e12c2ea1f2d266a2a1c2373ab3b57d925c6102646e4364f0d5a9f4'
        '7b7d10932a3c9b07ae10870c7830cacb2dd7164ec3e13ca419a931d6702c496b3ca2b7430f52aa3e71727b8d5f089f174ab13b1c69698925b2e37f8634e3fbff'
        '57413ec0582254216196c52cddc7c4635c61efcd7a2b538dd61628960b7bc91c09160bd39d6383c69ae470d4d3b6a9a6b4fb67f457ac91996bbcfebb8762880b'
        'a8092d2bdd6b72b2072178ae620f0d16fe93f17061e3ef789cba2ca5eb236d7277b78ea4c7838053ebd08b025c2255ac6ce2f5587c34e8dde04fe4a04f2e1fa3'
        '9e1f8221c14c1c7a3a7046321e850f92c12ff8782bf09e4f358b9a1912fd41791fec2eb56c81ae416b895ae8fd7176495d63da3c4a2f0f24eb97bd2e4e422b5f')

prepare() {
  # Extract AppImage
  chmod +x "${srcdir}/${_appname}-${_appver}.AppImage"
  "${srcdir}/${_appname}-${_appver}.AppImage" --appimage-extract

  pushd "${srcdir}/squashfs-root/usr/bin"
    # Use system ffmpeg
    rm -fv libav*.so.*

    # Use system sdl2
    rm -fv libSDL2.so

    # Remove pdb files
    rm -fv *.pdb
  popd

  # Create icons
  mkdir -v "${srcdir}/icons"
  pushd "${srcdir}/icons"
    magick -verbose "${srcdir}/squashfs-root/usr/bin/lazer.ico" osu.png
    magick -verbose "${srcdir}/squashfs-root/usr/bin/beatmap.ico" beatmap.png

    eval $(magick identify -format "mv -v %f osu-%G;" osu*.png)
    eval $(magick identify -format "mv -v %f beatmap-%G;" beatmap*.png)

    for icon in $srcdir/squashfs-root/usr/share/icons/hicolor/*/apps/osu.png; do
      mv -v "${icon}" "osu-$(echo "${icon}" | sed 's/^.*\/\([0-9]\{2,4\}x[0-9]\{2,4\}\)\/.*$/\1/g')"
    done
  popd
}

package() {
  # Install osu and libraries
  install -vdm755 "${pkgdir}/usr/lib"
  mv "${srcdir}/squashfs-root/usr/bin" "${pkgdir}/usr/lib/${_appname}"

  # Install launch script
  install -vDm755 "${srcdir}/${_appname}.bash" "${pkgdir}/usr/bin/${_appname}"

  # Install license file
  install -vDm644 "${srcdir}/${_resname}-LICENCE-${_resver}.md" "${pkgdir}/usr/share/licenses/${pkgname}/${_resname}-LICENCE.md"

  # Install desktop and mime files
  install -vDm644 "${srcdir}/${_appname}.desktop" "${pkgdir}/usr/share/applications/${_appname}.desktop"
  install -vDm644 "${srcdir}/${_appname}.xml" "${pkgdir}/usr/share/mime/packages/${_appname}.xml"

  # Install icons
  pushd "${srcdir}/icons"
    for icon in osu-*; do
      install -vDm644 "${icon}" "${pkgdir}/usr/share/icons/hicolor/$(echo "${icon}" | cut -d- -f2 -)/apps/${_appname}.png"
    done

    for icon in beatmap-*; do
      install -vDm644 "${icon}" "${pkgdir}/usr/share/icons/hicolor/$(echo "${icon}" | cut -d- -f2 -)/mimetypes/${_appname}-beatmap.png"
    done
  popd
}
