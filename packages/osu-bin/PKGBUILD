# Maintainer: Puqns67 <me@puqns67.icu>
# Contributor: David Husička <contact@bydave.net>
# Contributor: Tyler Nelson <neo@cybercat.cc>
# Contributor: Tuhana GAYRETLİ <tuhana at tuta dot io>
# Contributor: Quenten Schoonderwoerd <ellie at nicecock dot eu>
# Contributor: Antti <antti@antti.codes>

# This PKGBUILD is base on the AUR repo osu-lazer-bin, from these contributors above

_appname='osu'
_appver=2024.521.2
_resname='osu-resources'
_resver=2024.517.0

pkgname="${_appname}-bin"
pkgver="${_appver}"
pkgrel=2
pkgdesc="A free-to-win rhythm game. Rhythm is just a click away!"
arch=('x86_64')
url='https://osu.ppy.sh/'
license=('MIT' 'CC-BY-NC-4.0')
depends=('bash' 'ffmpeg4.4' 'gcc-libs' 'glibc' 'lttng-ust2.12' 'zlib')
provides=("${_appname}" 'osu-lazer')
conflicts=("${_appname}" 'osu-lazer')
replaces=('osu-lazer')
options=('!debug')

source=("${_appname}-${_appver}.AppImage"::"https://github.com/ppy/osu/releases/download/${_appver}/osu.AppImage"
        "${_resname}-LICENCE-${_resver}.md"::"https://github.com/ppy/osu-resources/raw/${_resver}/LICENCE.md"
        "${_appname}.bash"
        "${_appname}.desktop"
        "${_appname}.xml")

b2sums=('c367473a3bdd21e251183a68b9c6f6bb59140267772d68210f33886ea12eee8fb92abb3043cfddf93a388c80e5ddb6a8f084ac90aab84a28d8ef392df620e960'
        '7b7d10932a3c9b07ae10870c7830cacb2dd7164ec3e13ca419a931d6702c496b3ca2b7430f52aa3e71727b8d5f089f174ab13b1c69698925b2e37f8634e3fbff'
        '63c4f291210a06d1116689ba073fd370ab72db8bd951cf445da447d39a2c05492cebd37c293bf72519e6c86efb97600a9ac146d31ba29b8b6543cc50bcb9c6e9'
        'c1f20062245a02bdaef45ee0eb18c6eefd1faa09b6354d3dc83951e844cdf72a1832fdacad05139c1cfa54afd6ac0e0912971a7f4c324d4e5a6c681e833df1e7'
        '06878f8aa3368d73fb72d17cdf5b95c55c5d9f721ea5fcb5db6b538527a71746a874cb58619dbca8c6f43e402ebfdc414a80dc1d515288ddf9151a4d6bcd8a1f')

prepare() {
  # Extract AppImage
  chmod +x "${_appname}-${_appver}.AppImage"
  "${srcdir}/${_appname}-${_appver}.AppImage" --appimage-extract

  cd "${srcdir}/squashfs-root/usr/bin"

  # Use system libraries
  rm -v libav*.so.*

  # Remove pdb files
  rm -v *.pdb
}

package() {
  # Install osu and libraries
  install -dm755 "${pkgdir}/usr/lib"
  mv -v "${srcdir}/squashfs-root/usr/bin" "${pkgdir}/usr/lib/${_appname}"

  # Install launch script
  install -Dm755 "${srcdir}/${_appname}.bash" "${pkgdir}/usr/bin/${_appname}"

  # Install license file
  install -Dm644 "${srcdir}/${_resname}-LICENCE-${_resver}.md" "${pkgdir}/usr/share/licenses/${pkgname}/${_resname}-LICENCE-${_resver}.md"

  # Install icon, desktop and mime files
  install -Dm644 "${srcdir}/squashfs-root/osu!.png" "${pkgdir}/usr/share/pixmaps/${_appname}.png"
  install -Dm644 "${srcdir}/${_appname}.desktop" "${pkgdir}/usr/share/applications/${_appname}.desktop"
  install -Dm644 "${srcdir}/${_appname}.xml" "${pkgdir}/usr/share/mime/packages/${_appname}.xml"
}
